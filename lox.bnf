program        ::= declaration* EOF ;

declaration    ::= varDecl
               | statement ;

statement      ::= exprStmt
               | ifStmt
               | printStmt
               | whileStmt
               | block ;

ifStmt         ::= "if" "(" expression ")" statement ( "else" statement )? ;
whileStmt      ::= "while" "(" expression ")" statement ;
block          ::= "{" declaration* "}" ;

varDecl        ::= "var" IDENTIFIER ( "=" expression )? ";" ;
printStmt      ::= "print" expression ";" ;
exprStmt       ::= expression ";" ;

expression     ::= assignment ;
assignment     ::= IDENTIFIER "=" assignment | equality ;
equality       ::= comparison ( ( "!=" | "==" ) comparison )* ;
comparison     ::= addition ( ( ">" | ">=" | "<" | "<=" ) addition )* ;
addition       ::= multiplication ( ( "-" | "+" ) multiplication )* ;
multiplication ::= unary ( ( "/" | "*" ) unary )* ;
unary          ::= ( "!" | "-" ) unary
               |   primary ;
primary        ::= NUMBER | STRING | "false" | "true" | "nil"
               |   "(" expression ")"
               |   IDENTIFIER ;
